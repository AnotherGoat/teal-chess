import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'jacoco'
    id 'info.solidsoft.pitest' version "$pitestPluginVersion"
}

jacoco {
    toolVersion = jacocoVersion
}

pitest {
    pitestVersion = "$project.pitestVersion"
    junit5PluginVersion = "$project.junit5PluginVersion"

    // Excludes running mutations on perft tests
    targetTests = ['com.vmardones.tealchess.*.*']
}

tasks {
    test {
        useJUnitPlatform()

        if (!project.hasProperty('includePerft')) {
            exclude('com/vmardones/tealchess/PerftTest.class')
        }

        testLogging {
            exceptionFormat = TestExceptionFormat.FULL
        }

        // Generate a report after completing all the tests
        finalizedBy = [jacocoTestReport]
    }

    jacocoTestReport {
        reports {
            // XML reports are required to make Sonarqube understand the results
            xml.required.set(true)
            csv.required.set(true)
        }
    }
}

dependencies {
    // Unit testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    // Mocking
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

    // Better assertions
    testImplementation "org.assertj:assertj-core:$assertjVersion"

    // Testing equality
    testImplementation "nl.jqno.equalsverifier:equalsverifier:$equalsVerifierVersion"
}
