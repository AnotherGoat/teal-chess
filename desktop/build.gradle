plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.beryx.runtime' version '1.13.0'
}

sourceSets.main {
    resources.srcDirs = ['../assets/']
}

project.ext {
    applicationName = 'teal-chess'
    mainClassName = 'com.vmardones.tealchess.DesktopLauncher'
}

application {
    mainClass = mainClassName
}

runtime {
    // Bind services is need to avoid "Could not reconfigure JMX" exception
    options = ['--compress', '2', '--no-header-files', '--no-man-pages', '--bind-services']

    // Specifying modules is needed for now (https://github.com/beryx/badass-runtime-plugin/issues/136)
    modules = ['java.desktop']

    launcher {
        noConsole = true
    }

    jpackage {
        mainJar = "${applicationName}.jar"
        imageName = applicationName
        installerName = applicationName
        installerType = 'rpm'
    }
}

shadowJar {
    archiveBaseName = applicationName
    archiveVersion = ''
    archiveClassifier = ''
}

tasks {
    jar {
        manifest {
            attributes 'Main-Class': mainClassName
            attributes 'Implementation-Title': project.name
            attributes 'Implementation-Version': project.version
        }
    }
}

// Reproducible JAR builds (https://imperceptiblethoughts.com/shadow/configuration/reproducible-builds/)
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

dependencies {
    implementation project(':core')

    // Transcoding SVG files
    implementation "org.apache.xmlgraphics:batik-transcoder:$batikVersion"
    implementation "org.apache.xmlgraphics:batik-codec:$batikVersion"

    // CLI
    implementation "info.picocli:picocli:$picocliVersion"
    annotationProcessor "info.picocli:picocli-codegen:$picocliVersion"

    // Game engine
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}
