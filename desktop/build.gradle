plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.beryx.runtime' version '1.13.0'
}

sourceSets.main {
    java.srcDirs = ['src/']
    resources.srcDirs = ['../assets/']
}

application {
    mainClass = 'com.vmardones.tealchess.Chess'
    applicationName = 'teal-chess'
}

runtime {
    // Bind services is need to avoid "Could not reconfigure JMX" exception
    options = ['--compress', '2', '--no-header-files', '--no-man-pages', '--bind-services']

    // Specifying modules is needed for now (https://github.com/beryx/badass-runtime-plugin/issues/136)
    modules = ['java.desktop']

    launcher {
        noConsole = true
    }

    jpackage {
        mainJar = 'teal-chess.jar'
        imageName = 'teal-chess'
        installerName = 'teal-chess'
        installerType = 'rpm'
    }
}

shadowJar {
    archiveBaseName = 'teal-chess'
    archiveVersion = ''
    archiveClassifier = ''
}

tasks {
    jar {
        manifest {
            attributes 'Main-Class': 'com.vmardones.tealchess.Chess'
            attributes 'Implementation-Title': project.name
            attributes 'Implementation-Version': project.version
        }
    }
}

// Reproducible JAR builds (https://imperceptiblethoughts.com/shadow/configuration/reproducible-builds/)
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

dependencies {
    implementation project(':core')

    // Nullability
    compileOnly "org.eclipse.jdt:org.eclipse.jdt.annotation:$jdtVersion"

    // Logging
    implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"

    // Transcoding SVG files
    implementation "org.apache.xmlgraphics:batik-transcoder:$batikVersion"
    implementation "org.apache.xmlgraphics:batik-codec:$batikVersion"

    // CLI
    implementation "info.picocli:picocli:$picocliVersion"
    annotationProcessor "info.picocli:picocli-codegen:$picocliVersion"

    // GUI
    implementation "com.miglayout:miglayout-swing:$migLayoutVersion"
    implementation "com.formdev:flatlaf:$flatlafVersion"
}
