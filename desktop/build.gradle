plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version "$shadowVersion"
    id 'org.beryx.runtime' version "$beryxRuntimeVersion"
}

sourceSets.main {
    resources.srcDirs = ['../assets/']
}

project.ext {
    applicationName = 'teal-chess'
    mainClassName = 'com.vmardones.tealchess.DesktopLauncher'
}

application {
    mainClass = mainClassName
}

runtime {
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']

    // Specifying modules is needed for now (https://github.com/beryx/badass-runtime-plugin/issues/136)
    // java.desktop avoids java.lang.ClassNotFoundException: org.beryx.jlink.shadow.asm.ClassVisitor
    // jdk.unsupported avoids java.lang.NoClassDefFoundError: sun/misc/Unsafe
    modules = ['java.desktop', 'jdk.unsupported']

    launcher {
        noConsole = true
    }

    jpackage {
        mainJar = "${applicationName}.jar"
        imageName = applicationName
        installerName = applicationName
        installerType = 'rpm'
    }
}

shadowJar {
    archiveBaseName = applicationName
    archiveVersion = ''
    archiveClassifier = ''
}

tasks {
    jar {
        manifest {
            attributes 'Main-Class': mainClassName
            attributes 'Implementation-Title': project.name
            attributes 'Implementation-Version': project.version
        }
    }
}

// Reproducible JAR builds (https://imperceptiblethoughts.com/shadow/configuration/reproducible-builds/)
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

dependencies {
    implementation project(':core')

    // Game engine
    api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    api "com.badlogicgames.gdx:gdx-lwjgl3-glfw-awt-macos:$gdxVersion"

    // Loading fonts
    api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
}
