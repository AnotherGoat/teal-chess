import com.diffplug.spotless.java.PalantirJavaFormatStep

plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.18.0'
    id 'net.ltgt.errorprone' version '3.1.0'
    id 'org.sonarqube' version '4.0.0.2929'
}

defaultTasks 'compileJava'

allprojects {
    group = 'com.vmardones'
    version = '0.0'

    ext {
        palantirFormatVersion = '2.30.0'
        jacocoVersion = '0.8.10'

        jdtVersion = '2.2.700'
        errorProneVersion = '2.18.0'
        nullAwayVersion = '0.10.10'
        errorProneSupportVersion = '0.9.0'

        junitVersion = '5.9.3'
        mockitoVersion = '5.3.1'
        assertjVersion = '3.24.2'
        equalsVerifierVersion = '3.14.1'

        batikVersion = '1.16'
        picocliVersion = '4.7.3'
        gdxVersion = '1.11.0'
    }

    apply plugin: 'java'

    java.sourceCompatibility = JavaVersion.VERSION_17

    apply plugin: 'com.diffplug.spotless'

    spotless {
        java {
            palantirJavaFormat(palantirFormatVersion)
            importOrder('java|javax', 'org.apache', '')
            licenseHeader('/*\n' +
                    ' * Copyright (C) $YEAR  VÃ­ctor Mardones\n' +
                    ' * The full notice can be found at README.md in the root directory.\n' +
                    ' */\n\n')
            formatAnnotations()
        }
    }

    tasks {
        compileJava {
            dependsOn = [spotlessJavaApply]
        }
    }

    apply plugin: 'net.ltgt.errorprone'

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add("-Xlint:all")

        options.errorprone {
            option('NullAway:AnnotatedPackages', project.group)
            option('NullAway:ExcludedClassAnnotations', 'com.vmardones.tealchess.ExcludeFromNullAway')
            option('NullAway:KnownInitializers', 'com.badlogic.gdx.ApplicationAdapter.create')
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // Nullability
        compileOnly "org.eclipse.jdt:org.eclipse.jdt.annotation:$jdtVersion"

        // Linting
        errorprone "com.google.errorprone:error_prone_core:$errorProneVersion"
        errorprone "com.uber.nullaway:nullaway:$nullAwayVersion"
        errorprone "tech.picnic.error-prone-support:error-prone-contrib:$errorProneSupportVersion"
    }
}
