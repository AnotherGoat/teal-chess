plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'net.ltgt.errorprone' version '4.0.1'
    id 'org.sonarqube' version '5.1.0.4882'
}

defaultTasks 'compileJava'

allprojects {
    group = 'com.vmardones'
    version = '0.0'

    ext {
        palantirFormatVersion = '2.50.0'
        jacocoVersion = '0.8.12'

        jdtVersion = '2.3.0'
        errorProneVersion = '2.31.0'
        nullAwayVersion = '0.11.2'
        errorProneSupportVersion = '0.18.0'

        junitVersion = '5.11.0'
        mockitoVersion = '5.13.0'
        assertjVersion = '3.26.3'
        equalsVerifierVersion = '3.16.2'

        pitestPluginVersion = '1.15.0'
        pitestVersion = '1.16.2'
        junit5PluginVersion = '1.2.1'

        batikVersion = '1.17'
        gdxVersion = '1.12.1'

        shadowVersion = '8.3.0'
        beryxRuntimeVersion = '1.13.1'
    }

    apply plugin: 'java'

    java.sourceCompatibility = JavaVersion.VERSION_21

    apply plugin: 'com.diffplug.spotless'

    spotless {
        java {
            palantirJavaFormat(palantirFormatVersion)
            importOrder('java|javax', 'org.apache', '')
            licenseHeader('/*\n' +
                    ' * Copyright (C) $YEAR  VÃ­ctor Mardones\n' +
                    ' * The full notice can be found at README.md in the root directory.\n' +
                    ' */\n\n')
            formatAnnotations()
        }
    }

    tasks {
        compileJava {
            dependsOn = [spotlessJavaApply]
        }
    }

    apply plugin: 'net.ltgt.errorprone'

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add("-Xlint:all")

        options.errorprone {
            disable('JavaTimeDefaultTimeZone', 'TimeZoneUsage')
            option('NullAway:AnnotatedPackages', project.group)
            option('NullAway:ExcludedClassAnnotations', 'com.vmardones.tealchess.ExcludeFromNullAway')
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // Nullability
        compileOnly "org.eclipse.jdt:org.eclipse.jdt.annotation:$jdtVersion"

        // Linting
        errorprone "com.google.errorprone:error_prone_core:$errorProneVersion"
        errorprone "com.uber.nullaway:nullaway:$nullAwayVersion"
        errorprone "tech.picnic.error-prone-support:error-prone-contrib:$errorProneSupportVersion"
    }
}
